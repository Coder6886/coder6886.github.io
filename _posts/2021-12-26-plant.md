---
layout:     post
title:      植物工厂
subtitle:   
date:       2021-12-26
author:     coder6886
header-img: img/post-bg-ios9-web.jpg
catalog: true
tags:
    - Arduino
---
创建一个文件夹，名叫main.Ink，然后在里面分别创造以下三个文件：

main.Ink.ino:

```c++
#include<ESP8266WiFi.h>
#include<PubSubClient.h>
#include<ArduinoJson.h>
#include "aliyun_mqtt.h"
#include<Adafruit_NeoPixel.h>
#include<DHT.h>
#define Light 13
#define NUMPIXELS 4
#ifdef __AVR__
#include <avr/power.h>
#endif

#define msensor A0
#define pump 5
#define dhtpin 15
#define DHTTYPE DHT11

#define WIFI_SSID        "**************"//替换自己的WIFI
#define WIFI_PASSWD      "**********"//替换自己的WIFI

#define PRODUCT_KEY      "**********" //替换自己的PRODUCT_KEY
#define DEVICE_NAME      "*************" //替换自己的DEVICE_NAME
#define DEVICE_SECRET    "**********************"//替换自己的DEVICE_SECRET

const unsigned leastmoisture = 0;
const unsigned timewater = 2000;

#define DEV_VERSION       "1.0.0"        //固件版本信息

#define ALINK_BODY_FORMAT         "{\"id\":\"123\",\"version\":\"1.0.0\",\"method\":\"%s\",\"params\":%s}"
#define ALINK_TOPIC_PROP_POST     "/sys/" PRODUCT_KEY "/" DEVICE_NAME "/thing/event/property/post"
#define ALINK_TOPIC_PROP_POSTRSP  "/sys/" PRODUCT_KEY "/" DEVICE_NAME "/thing/event/property/post_reply"
#define ALINK_TOPIC_PROP_SET      "/sys/" PRODUCT_KEY "/" DEVICE_NAME "/thing/service/property/set"
#define ALINK_TOPIC_USER_GET      "/" PRODUCT_KEY "/" DEVICE_NAME "/user/get"
#define ALINK_METHOD_PROP_POST    "thing.event.property.post"
#define ALINK_TOPIC_DEV_INFO      "/ota/device/inform/" PRODUCT_KEY "/" DEVICE_NAME ""
#define ALINK_VERSION_FROMA      "{\"id\": 123,\"params\": {\"version\": \"%s\"}}"

unsigned long lastMs = 0,pumpMs = 0;
float humidity,temperature;
WiFiClient   espClient;
PubSubClient mqttClient(espClient);
Adafruit_NeoPixel pixels = Adafruit_NeoPixel(NUMPIXELS, Light, NEO_GRB + NEO_KHZ800);
DHT dht(dhtpin,DHTTYPE);
const uint8_t red = 220, green = 200, blue = 170, bright = 255;
bool watering = false;
void setcolour(uint8_t r_v,uint8_t g_v,uint8_t b_v,uint8_t bright_v){
  pixels.setBrightness(bright_v);

  for(int i = 0; i < NUMPIXELS; i++){
      pixels.setPixelColor(i, r_v, g_v, b_v);
  }
  pixels.show();
}
void init_wifi(const char *ssid, const char *password)      //连接WiFi
{
  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED)
  {
    Serial.println("WiFi does not connect, try again ...");
    delay(500);
  }

  Serial.println("Wifi is connected.");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}
void mqtt_callback(char *topic, byte *payload, unsigned int length) //mqtt回调函数
{
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");
  payload[length] = '\0';
  Serial.println((char *)payload);
  // https://arduinojson.org/v5/assistant/  json数据解析网站

  Serial.println("   ");
  Serial.println((char *)payload);
  Serial.println("   ");
  if(strstr(topic, ALINK_TOPIC_PROP_SET)){
    StaticJsonBuffer<1000> jsonBuffer;
    JsonObject &root = jsonBuffer.parseObject(payload);
    int params_water = root["params"]["water"] | -1;
    int params_light = root["params"]["openlight"] | -1;
    if(!root.success()){
      Serial.println("parse failed");
      return;
    }
    if(params_water == 1){
      watering = true;
      pumpMs = millis();
      digitalWrite(pump,HIGH);
    }
    if(params_light == 1){
      setcolour(red,green,blue,bright);
    }
    else{
      setcolour(0,0,0,0);
    }
  }
}
void mqtt_version_post()
{
  char param[512];
  char jsonBuf[1024];

  sprintf(param, "{\"id\": 123,\"params\": {\"version\": \"%s\"}}", DEV_VERSION);
  Serial.println(param);
  mqttClient.publish(ALINK_TOPIC_DEV_INFO, param);
}
void mqtt_check_connect()
{
  while (!mqttClient.connected())//
  {
    while (connect_aliyun_mqtt(mqttClient, PRODUCT_KEY, DEVICE_NAME, DEVICE_SECRET))
    {
      Serial.println("MQTT connect succeed!");
      mqttClient.subscribe(ALINK_TOPIC_PROP_SET);
      mqttClient.subscribe(ALINK_TOPIC_USER_GET);
      Serial.println(ALINK_TOPIC_PROP_SET);
      Serial.println("subscribe done");
      mqtt_version_post();
    }
  }

}
void mqtt_interval_post()
{
  char param[512];
  char jsonBuf[1024];

  sprintf(param, "{\"soilHumidity\":%d,\"airTemp\":%d,\"airHumidity\":%d}", analogRead(msensor), (int)temperature, (int)humidity);
  sprintf(jsonBuf, ALINK_BODY_FORMAT, ALINK_METHOD_PROP_POST, param);
  Serial.println(jsonBuf);
  mqttClient.publish(ALINK_TOPIC_PROP_POST, jsonBuf);

  sprintf(param, "{\"water\":%d}", 1);
  sprintf(jsonBuf, ALINK_BODY_FORMAT, ALINK_METHOD_PROP_POST, param);
  Serial.println(jsonBuf);
 // mqttClient.publish(ALINK_TOPIC_PROP_POST, jsonBuf);
}
void setup() {
  // put your setup code here, to run once:
  Serial.begin(115200);
  init_wifi(WIFI_SSID, WIFI_PASSWD);
  pixels.begin();
  pinMode(pump,OUTPUT);
  mqttClient.setCallback(mqtt_callback);
  mqtt_check_connect();
  humidity=0;
  temperature=0;
}
void loop() {
  // put your main code here, to run repeatedly:
 // while(1){
//    delay(1000);
//    Serial.println(analogRead(msensor));
    if(millis()-lastMs >= 10000){
      lastMs = millis();
      Serial.println(analogRead(msensor));
      if(analogRead(msensor) <= leastmoisture){
        watering = true;
        pumpMs = millis();
        digitalWrite(pump,HIGH);
      }
      float h = dht.readHumidity();
      float t = dht.readTemperature();
      if (isnan(h) || isnan(t)){
        Serial.println("Failed to read from DHT sensor!");
        lastMs = millis();
        return;
      }
      
      Serial.println("Read from DHT11:");
      Serial.println("Humidity::");
      Serial.println(h);
      Serial.println("Temp::");
      Serial.println(t);
      humidity = h;
      temperature = t;
      mqtt_check_connect();
      mqtt_interval_post();
    }
    if(watering && millis()-pumpMs >= timewater){
      watering = false;
      digitalWrite(pump,LOW);
    }
    delay(1);
  mqttClient.loop();
}

```

aliyun_mqtt.cpp:

```c++
/*
  Aliyun_mqtt.h - Library for connect to Aliyun MQTT server.
*/

#include "aliyun_mqtt.h"

#include <SHA256.h>

#define MQTT_PORT 1883
#define SHA256HMAC_SIZE 32

// Verify tool: http://tool.oschina.net/encrypt?type=2
static String hmac256(const String &signcontent, const String &ds)
{
  byte hashCode[SHA256HMAC_SIZE];
  SHA256 sha256;

  const char *key = ds.c_str();
  size_t keySize = ds.length();

  sha256.resetHMAC(key, keySize);
  sha256.update((const byte *)signcontent.c_str(), signcontent.length());
  sha256.finalizeHMAC(key, keySize, hashCode, sizeof(hashCode));

  String sign = "";
  for (byte i = 0; i < SHA256HMAC_SIZE; ++i)
  {
    sign += "0123456789ABCDEF"[hashCode[i] >> 4];
    sign += "0123456789ABCDEF"[hashCode[i] & 0xf];
  }

  return sign;
}

static String mqttBroker;
static String mqttClientID;
static String mqttUserName;
static String mqttPassword;

// call this function once
void mqtt_prepare(const char *timestamp,const char *productKey, const char *deviceName,const char *deviceSecret,const char *region)
{
  mqttBroker = productKey;
  mqttBroker += ".iot-as-mqtt.";
  mqttBroker += String(region);
  mqttBroker += ".aliyuncs.com";
  
  // Serial.println(mqttBroker);

  mqttUserName = deviceName;
  mqttUserName += '&';
  mqttUserName += productKey;
   //Serial.println(mqttUserName);
   
  mqttClientID = deviceName; // device name used as client ID
  mqttClientID += "|securemode=3,signmethod=hmacsha256,timestamp=";
  mqttClientID += timestamp;
  mqttClientID += '|';
   //Serial.println(mqttClientID);
}

bool connect_aliyun_mqtt_With_password(PubSubClient &mqttClient, const char *password)
{
  mqttClient.setServer(mqttBroker.c_str(), MQTT_PORT);

  byte mqttConnectTryCnt = 5;
  while (!mqttClient.connected() && mqttConnectTryCnt > 0)
  {
    Serial.println("Connecting to MQTT Server ...");
    Serial.println(mqttClientID.c_str());
    Serial.println( mqttUserName.c_str());
    Serial.println(password);
    if (mqttClient.connect(mqttClientID.c_str(), mqttUserName.c_str(), password))
    {

      // Serial.println("MQTT Connected!");
      return true;
    }
    else
    {
      byte errCode = mqttClient.state();
      Serial.print("MQTT connect failed, error code:");
      Serial.println(errCode);
      if (errCode == MQTT_CONNECT_BAD_PROTOCOL || errCode == MQTT_CONNECT_BAD_CLIENT_ID || errCode == MQTT_CONNECT_BAD_CREDENTIALS || errCode == MQTT_CONNECT_UNAUTHORIZED)
      {
        Serial.println("No need to try again.");
        break; // No need to try again for these situation
      }
      delay(3000);
    }
    mqttConnectTryCnt -= 1;
  }

  return false;
}

bool connect_aliyun_mqtt(
    PubSubClient &mqttClient,
    const char *productKey,
    const char *deviceName,
    const char *deviceSecret,
    const char *region)
{
  String timestamp = String(millis());
  mqtt_prepare(timestamp.c_str(), productKey, deviceName, deviceSecret, region);

  // Generate MQTT Password, use deviceName as clientID
  String signcontent = "clientId";
  signcontent += deviceName;
  signcontent += "deviceName";
  signcontent += deviceName;
  signcontent += "productKey";
  signcontent += productKey;
  signcontent += "timestamp";
  signcontent += timestamp;

  String mqttPassword = hmac256(signcontent, deviceSecret);

   //Serial.print("HMAC256 data: ");
   //Serial.println(signcontent);
   //Serial.print("HMAC256 key: ");
  // Serial.println(deviceSecret);
  // Serial.println(mqttPassword);

  return connect_aliyun_mqtt_With_password(mqttClient, mqttPassword.c_str());
}

```

aliyun_mqtt.h:

```c++
/*
  Aliyun_mqtt.h - Library for connect to Aliyun MQTT server with authentication by
  product key, device name and device secret.

  https://www.alibabacloud.com/help/product/30520.htm
*/

#ifndef _ALIYUN_MATT_H
#define _ALIYUN_MATT_H

#include <Arduino.h>
#include <PubSubClient.h>

/**
 * Connect to Alibaba Cloud MQTT server. In connection process, it will try several times for
 * possible network failure. For authentication issue, it will return false at once.
 *
 * @param mqttClient: Caller provide a valid PubSubClient object (initialized with network client).

 * @param productKey: Product Key, get from Alibaba Cloud Link Platform.

 * @param deviceName: Device Name, get from Alibaba Cloud Link Platform.

 * @param deviceSecret: Device Secret, get from Alibaba Cloud Link Platform.
 *
 * @param region: Optional region, use "cn-shanghai" as default. It can be "us-west-1",
 *                "ap-southeast-1" etc. Refer to Alibaba Cloud Link Platform.
 *
 *
 * @return true if connect succeed, otherwise false.
 */
extern "C" bool connect_aliyun_mqtt(
    PubSubClient &mqttClient,
    const char *productKey,
    const char *deviceName,
    const char *deviceSecret,
    const char *region = "cn-shanghai");

/**
 * Two new added APIs are designed for devices with limited resource like Arduino UNO.
 * Since it is hard to calculate HMAC256 on such devices, the calculation can be done externally.
 *
 * These two APIs should be used together with external HMAC256 calculation tools, e.g.
 * http://tool.oschina.net/encrypt?type=2
 * They can be used together to replace connectAliyunMQTT on resource-limited devices.
 */

/**
 * This API should be called in setup() phase to init all MQTT parameters. Since HMAC256
 * calculation is executed extenrally, a fixed timestamp string should be provided, such
 * as "23668" etc. The same timestamp string is also used to calculate HMAC256 result.
 *
 * Other params are similar to them in connectAliyunMQTT.
 */
extern "C" void mqtt_prepare(
    const char *timestamp,
    const char *productKey,
    const char *deviceName,
    const char *deviceSecret,
    const char *region = "cn-shanghai");

/**
 * Use tools here to calculate HMAC256: http://tool.oschina.net/encrypt?type=2
 * The calculated result should be defined as constants and passed when call this function.
 */
extern "C" bool connect_aliyun_mqtt_With_password(PubSubClient &mqttClient, const char *password);

#endif

```

然后运行main.Ink.ino
